Properties versions = new Properties()
versions.load(new FileInputStream("app/version.properties"))

apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"
    defaultConfig {
        applicationId "com.gaborbiro.sharedexpenses"
        minSdkVersion 21
        targetSdkVersion 22
        versionCode 1001
        versionName "1.1"
        vectorDrawables.generatedDensities = ['xxhdpi']
    }
    buildTypes {
        debug {
            minifyEnabled false
            useProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            applicationIdSuffix ".debug"
        }
        release {
            minifyEnabled true
            useProguard true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            applicationIdSuffix ".release"
        }
        applicationVariants.all { variant ->
            variant.outputs.each { output ->
                def version = variant.versionName + "(" + variant.versionCode + ")"
                def apkPostfix = "-" + version + ".apk"
                output.outputFile = new File(
                        output.outputFile.parent,
                        output.outputFile.name.replace(".apk", apkPostfix))
            }
        }
    }
    dexOptions {
        javaMaxHeapSize "4g"
    }
    lintOptions {
        abortOnError false
        disable 'InvalidPackage'
    }
    dataBinding {
        enabled = true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile "com.android.support:appcompat-v7:${versions.getProperty('SUPPORT_LIBRARY')}"
    compile "com.android.support:design:${versions.getProperty('SUPPORT_LIBRARY')}"

    compile "com.google.android.gms:play-services-auth:${versions.getProperty('PLAY_SERVICES')}"
    compile 'pub.devrel:easypermissions:0.2.1'
    compile('com.google.api-client:google-api-client-android:1.22.0') {
        exclude group: 'org.apache.httpcomponents'
    }
    compile('com.google.apis:google-api-services-sheets:v4-rev38-1.22.0') {
        exclude group: 'org.apache.httpcomponents'
    }
    compile "com.google.android.gms:play-services-drive:${versions.getProperty('PLAY_SERVICES')}"
    compile "io.reactivex:rxjava:${versions.getProperty('RXJAVA')}"
    compile "io.reactivex:rxandroid:${versions.getProperty('RXANDROID')}"
    compile "com.google.dagger:dagger:${versions.getProperty('DAGGER')}"
    annotationProcessor "com.google.dagger:dagger-compiler:${versions.getProperty('DAGGER')}"
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.0'
    compile "com.jakewharton:butterknife:${versions.getProperty('BUTTERKNIFE')}"
    annotationProcessor "com.jakewharton:butterknife-compiler:${versions.getProperty('BUTTERKNIFE')}"
    compile 'com.afollestad.material-dialogs:core:0.9.1.0'

    provided "org.projectlombok:lombok:1.16.14"
    annotationProcessor "org.projectlombok:lombok:1.16.14"
}
