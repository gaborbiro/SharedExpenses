Properties versions = new Properties()
versions.load(new FileInputStream("$projectDir/version.properties"))

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 25
    buildToolsVersion "26.0.2"
    defaultConfig {
        applicationId "com.gaborbiro.sharedexpenses"
        minSdkVersion 23
        targetSdkVersion 23
        versionCode 1001
        versionName "1.1"
        vectorDrawables.generatedDensities = ['xxhdpi']
        multiDexEnabled true
    }
    buildTypes {
        debug {
            minifyEnabled false
            useProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            applicationIdSuffix ".debug"
        }
        release {
            minifyEnabled true
            useProguard true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            applicationIdSuffix ".release"
        }
        applicationVariants.all { variant ->
            variant.outputs.each { output ->
                def version = variant.versionName + "(" + variant.versionCode + ")"
                def apkPostfix = "-" + version + ".apk"
                output.outputFileName = output.outputFile.name.replace(".apk", apkPostfix)
            }
        }
    }

    flavorDimensions "default"

    dexOptions {
        dexInProcess true
    }
    lintOptions {
        abortOnError false
        disable 'InvalidPackage'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    packagingOptions {
        exclude 'LICENSE.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/INDEX.LIST'
        exclude 'META-INF/services/javax.annotation.processing.Processor'
        exclude 'META-INF/rxjava.properties'
    }
}

repositories {
    jcenter()
}

dependencies {
    implementation "com.android.support:appcompat-v7:${versions.getProperty('SUPPORT_LIBRARY')}"
    implementation "com.android.support:design:${versions.getProperty('SUPPORT_LIBRARY')}"
    implementation "com.google.android.gms:play-services-drive:${versions.getProperty('PLAY_SERVICES')}"
    implementation "com.google.android.gms:play-services-auth:${versions.getProperty('PLAY_SERVICES')}"
    implementation 'pub.devrel:easypermissions:0.2.1'
    implementation('com.google.api-client:google-api-client-android:1.22.0') {
        exclude group: 'org.apache.httpcomponents'
    }
    implementation('com.google.apis:google-api-services-sheets:v4-rev38-1.22.0') {
        exclude group: 'org.apache.httpcomponents'
    }
    implementation "io.reactivex.rxjava2:rxjava:${versions.getProperty('RXJAVA')}"
    implementation "io.reactivex.rxjava2:rxandroid:${versions.getProperty('RXANDROID')}"
    implementation "com.google.dagger:dagger:${versions.getProperty('DAGGER')}"
    kapt "com.google.dagger:dagger-compiler:${versions.getProperty('DAGGER')}"
    implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.4'
    implementation "com.jakewharton:butterknife:${versions.getProperty('BUTTERKNIFE')}"
    kapt "com.jakewharton:butterknife-compiler:${versions.getProperty('BUTTERKNIFE')}"
    implementation 'com.afollestad.material-dialogs:core:0.9.1.0'

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
}